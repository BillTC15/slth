5 ::REM slitherio time and score keeping
10 REM : CLS
20 CLEAR 1200
30 GOSUB 5000
400 REM input time and score
410 TM$="0000":SZ$="000000"
420 PRINT@274,SZ$;
530 PRINT@268,LEFT$(TM$,2);":";RIGHT$(TM$,2);
540 FM$=TM$:QU$="0123456789DTdt"+CHR$(8)+CHR$(13)+CHR$(27)+CHR$(30)
550 GOSUB 4100:FU$=I$:GOSUB 4400:I$=FU$
552 IF I$="D" THEN GOSUB 4200:GOTO 680
554 IF I$="T" THEN GOSUB 4250:GOTO 680
560 IF I$=CHR$(27) THEN GOTO 1000
570 IF I$=CHR$(8) THEN TM$="0"+LEFT$(TM$,3):GOTO 530
575 IF I$=CHR$(30) THEN EL=6:DL=-1:GOTO 1900
580 IF I$<>CHR$(13) THEN TM$=RIGHT$(TM$,3)+I$:GOTO 530
590 SOUND 4000,1:SOUND 3000,1 ::REM input score
600 PRINT @274,SZ$;
610 FM$=SZ$:QU$="0123456789"+CHR$(8)+CHR$(13)+CHR$(27)
620 GOSUB 4100
630 IF I$=CHR$(27) THEN GOTO 1000
640 IF I$=CHR$(8) THEN SZ$="0"+LEFT$(SZ$,5):GOTO 600
650 IF I$<>CHR$(13) THEN SZ$=RIGHT$(SZ$,5)+I$:GOTO 600
660 ::REM WRITE SCORE TO FILE, UPDATE DISPLAY SCORES
670 SOUND 3000,1:SOUND 4000,1:GOSUB 4300:FM$=TM$+SZ$:GOSUB 5800
680 GOSUB 5050:I$=INKEY$:I$=INKEY$:I$="":GOTO 400
685 REM [Q]uit
690 BEEP
1000 PRINT@200,"QUIT: ";
1010 GOSUB 4000
1020 IF I$<>"Y" THEN GOSUB 5050:GOTO 400
1030 BEEP:GOSUB 5600
1040 CLS:POKE 63038,0:MENU
1900 DS$=DS$(EL+8) : REM ARROWS TO SELECT EDIT LINE - ADD DATE/TIME SKIP
1902 IF INSTR(DS$,":") = 0 AND INSTR(DS$,"/") = 0 THEN DL=0:GOTO 1908
1904 EL=EL+DL: IF EL=0 THEN DL=1:GOTO 1904
1906 IF EL=7 THEN GOTO 400 ELSE GOTO 1900
1908 POKE63048,1:SZ$=RIGHT$(DS$,6):TM$=LEFT$(DS$,4)
1910 PRINT@EL*40-12,LEFT$(TM$,2);":";RIGHT$(TM$,2);" ";SZ$;
1930 I$=INKEY$:IFI$="" THEN GOTO 1930
1940 IF I$<>CHR$(30) THEN GOTO 1950
1944 POKE63048,0:PRINT@EL*40-12,LEFT$(TM$,2);":";RIGHT$(TM$,2);" ";SZ$;
1946 DL=-1:GOTO 1904
1950 IF I$<>CHR$(31) THEN GOTO 1960
1954 POKE63048,0:PRINT@EL*40-12,LEFT$(TM$,2);":";RIGHT$(TM$,2);" ";SZ$;
1956 DL=1:GOTO 1904
1960 IF I$=CHR$(13) THEN GOSUB 2000:GOTO 1900
1965 IF I$=CHR$(8) THEN GOSUB 4500:DL=-1:GOTO 1900
1970 IF I$<>CHR$(27) THEN GOTO 1930
1980 POKE63048,0:PRINT@EL*6-12,LEFT$(TM$,2);":";RIGHT$(TM$,2);" ";SZ$;
1990 DL=0:GOTO 30
2000 :: REM : previous entry editing
2020 PRINT@EL*40-6,SZ$;
2030 PRINT@EL*40-12,LEFT$(TM$,2);":";RIGHT$(TM$,2);
2040 FM$=TM$:QU$="0123456789DTdt"+CHR$(8)+CHR$(13)+CHR$(27)
2050 GOSUB 4100:FU$=I$:GOSUB 4400:I$=FU$
2052 IF I$="D" THEN GOSUB 4200:GOTO 2180
2054 IF I$="T" THEN GOSUB 4250:GOTO 2180
2060 IF I$=CHR$(27) THEN GOTO 2190
2070 IF I$=CHR$(8) THEN TM$="0"+LEFT$(TM$,3):GOTO 2030
2080 IF I$<>CHR$(13) THEN TM$=RIGHT$(TM$,3)+I$:GOTO 2030
2090 SOUND 4000,1:SOUND 3000,1 ::REM input score
2100 PRINT@EL*40-6,SZ$;
2110 FM$=SZ$:QU$="0123456789"+CHR$(8)+CHR$(13)+CHR$(27)
2120 GOSUB 4100
2130 IF I$=CHR$(27) THEN GOTO 1980
2140 IF I$=CHR$(8) THEN SZ$="0"+LEFT$(SZ$,5):GOTO 2100
2150 IF I$<>CHR$(13) THEN SZ$=RIGHT$(SZ$,5)+I$:GOTO 2100
2160 ::REM WRITE EDIT TO DS$(EL) RETURN TO LINE SELECTION
2170 SOUND 3000,1:SOUND 4000,1:FM$=TM$+SZ$:DS$(EL+8)=FM$:DL=0
2180 REM :
2190 I$=INKEY$:I$=INKEY$:I$="":GOTO 1900
3999 END
4000 BEEP:PRINT"Are you sure? (y/n)"
4010 I$=INKEY$:FU$=I$:GOSUB 4400:I$=FU$:IF I$<>"Y" AND I$<>"N" THEN 4010
4020 RETURN
4100 ::REM blinking cursor input, FM$ = entered text/ template, QU$=acceptable input
4110 PRINT CHR$(8);RIGHT$(FM$,1);CHR$(8);:CALL 16969
4114 I$=INKEY$:IF I$="" OR INSTR(QU$,I$)=0 THEN GOTO 4114
4116 ::REM
4118 FOR T=1 TO 6:CALL 30326:NEXT T
4120 CALL 16974:RETURN
4140 ::REM custom print using, FM$ = text to print, QU$= mask; @= char from FM$ other = char from QU$
4150 T=1:FOR X = 1 TO LEN(FM$)
4160 IF MID$(QU$,T,1)<>"@" THEN PRINT MID$(QU$,T,1);:T=T+1:GOTO 4160
4170 PRINT MID$(FM$,X,1):T=T+1
4180 NEXT X
4190 RETURN
4200 ::REM Put date to DS$()
4210 GOSUB 4300
4220 FM$=DATE$:GOSUB 5800
4240 RETURN
4250 ::REM Put time to DS$()
4260 GOSUB 4300
4270 FM$=TIME$:GOSUB 5800
4290 RETURN
4300 OPEN "RAM:SLTHR2.DO" FOR APPEND AS 1
4310 IF LEFT$(DS$(1),1)<>"X" THEN PRINT #1,DS$(1)
4320 CLOSE #1
4330 RETURN
4400 ::REM force uppercase fU$-> FU$
4405 IF FU$="" THEN RETURN
4410 FOR X = 1 TO LEN(FU$)
4420 T=ASC(MID$(FU$,X,1))
4430 MID$(FU$,X,1)=CHR$(T+((T>96)AND(T<123))*32)
4440 NEXT X
4450 RETURN
4500 REM : Delete entry DS$(EL+8)
4505 IF LEFT$(DS$(7),1)="X" THEN SOUND 3000,2:SOUND4000,1:RETURN
4510 FOR T=EL+8 TO 2 STEP -1:DS$(T)=DS$(T-1):NEXT T
4520 DS$(1)="XXXXXXXXXX"
4530 POKE63048,0:GOSUB 5065:RETURN
5000 REM : STARTUP
5002 CLS:PRINT @108,"Scorekeeper":PRINT @154,"v0.7":PRINT @188,"(c) 2025":PRINT @230,"g walker"
5006 DIM DS$(14):POKE 63038,1
5010 OPEN "RAM:SLTPR7.DO" FOR INPUT AS 1
5020 FOR T=1 TO 14:INPUT #1,DS$(T):NEXT T
5030 CLOSE 1
5050 ::REM DISPLAY LAST 6 ENTRIES ON SCREEN NEXT TO NEW BOX
5055 CALL 17001:PRINT @308,"Time";CHR$(152);" Score";CHR$(152);:CALL 17006
5060 LINE (166,0)-(166,63),1:LINE(167,56)-(167,63),1
5065 FOR T=9 TO 14:DS$=DS$(T)
5070 IF INSTR(DS$,":") <> 0 THEN PRINT @ 28+40*(T-9),"";:PRINT USING"at  \      \";DS$;:GOTO 5080
5075 IF INSTR(DS$,"/") <> 0 THEN PRINT @ 28+40*(T-9),"";:PRINT USING"\ \ \      \";DAY$,DS$;:GOTO 5080
5077 PRINT @ 28+40*(T-9),"";:PRINT USING"\\:\\ \    \";LEFT$(DS$,2),MID$(DS$,3,2),RIGHT$(DS$,6);
5080 NEXT T
5090 RETURN
5600 ::REM SAVE SET OF 14 LAST FROM DS$()
5610 OPEN "RAM:SLTPR7.DO" FOR OUTPUT AS 1
5620 FOR T=1 TO 14:PRINT #1,DS$(T):NEXT T
5630 CLOSE 1:RETURN
5800 ::REM ADD FM$ (TIME SCORE COMBO) TO DS$()
5810 FOR T=1 TO 13:DS$(T)=DS$(T+1):NEXT T
5820 DS$(14)=FM$
5830 RETURN
9000 REM : Data
9100 DATA Mon, Tue, Wed, Thu, Fri, Sat, Sun
9999 ::REM CHR$: 8 = BKSP, 13 = RTN, 27 = ESC
